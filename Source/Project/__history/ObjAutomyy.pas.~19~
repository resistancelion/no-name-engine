{$optimization off}

unit ObjAutoMyy;

interface
{$HPPEMIT LEGACYHPP}

uses System.Rtti,System.SysUtils, System.TypInfo, fmx.dialogs, Winapi.Windows, hzend_types;

{$IF Defined(CPUX86) and Defined(ASSEMBLER) and not Defined(PUREPASCAL)}
  {$DEFINE X86ASM}
{$ELSEIF Defined(CPUX64) and Defined(ASSEMBLER) and not Defined(PUREPASCAL)}
  {$DEFINE X64ASM}
{$ENDIF}

{$IF SizeOf(Extended) >= 10)} // 10,12,16
  {$DEFINE EXTENDEDHAS10BYTES}
{$ENDIF}

{$IF SizeOf(Extended) = 10)}
  {$DEFINE EXTENDEDIS10BYTES}
{$ENDIF}

{$IFDEF CPUX86}
const
  paEAX = Word(0);
  paEDX = Word(1);
  paECX = Word(2);
  paStack = Word(3);
{$ENDIF CPUX86}
{$IFDEF CPUX64}
const
  paRAX = Word(0);
  paRDX = Word(1);
  paRCX = Word(2);
  paStack = Word(3);

type
  prxarr = array[paRAX..paRDX] of Cardinal;
{$ENDIF}


{$IFDEF MSWINDOWS}
type
  IMethodHandler = interface
    ['{4E61C8CD-16CC-4830-B1E4-84F86FBC0D23}']
    function Execute(__PropType:TRttiProperty; const Args: TArray<TValue>): Variant;
  end;

  PParameters = ^TParameters;
  TParameters = packed record
{$IFDEF CPUX64}
    Registers: array[paRDX..paRCX] of Cardinal;
    EAXRegister: Cardinal;
    ReturnAddress: Pointer;
{$ENDIF CPUX64}
{$IFDEF CPUX86}
    Registers: array[paEDX..paECX] of Cardinal;
    EAXRegister: Cardinal;
    ReturnAddress: Pointer;
{$ENDIF CPUX86}
    Stack: array[0..1023] of Byte;
  end;

function CreateMethodPointer(const MethodHandler: IMethodHandler; __PropType:TRttiProperty): TMethod; overload;

procedure ReleaseMethodPointer(MethodPointer: TMethod);

{$ENDIF MSWINDOWS}


implementation

uses
  System.Variants, System.VarUtils, System.RTLConsts;


function GetTypeSize(TypeInfo: PTypeInfo): Integer;
var
  TypeData: PTypeData;
begin
  case TypeInfo^.Kind of
    tkChar:
      Result := 1;
    tkWChar:
      Result := 2;
    tkInteger, tkEnumeration:
      begin
        TypeData := GetTypeData(TypeInfo);
        if TypeData^.MinValue >= 0 then
          if Cardinal(TypeData^.MaxValue) > $FFFF then
            Result := 4
          else if TypeData^.MaxValue > $FF then
            Result := 2
          else
            Result := 1
        else
          if (TypeData^.MaxValue > $7FFF) or (TypeData^.MinValue < -$7FFF - 1) then
            Result := 4
          else if (TypeData^.MaxValue > $7F) or (TypeData^.MinValue < -$7F - 1) then
            Result := 2
          else
            Result := 1;
      end;
    tkFloat:
      begin
        TypeData := GetTypeData(TypeInfo);
        case TypeData^.FloatType of
          ftSingle: Result := 4;
          ftComp, ftCurr, ftDouble: Result := 8;
        else
          Result := -1;
        end;
      end;
    tkString, tkLString, tkUString, tkWString, tkInterface, tkClass:
      Result := SizeOf(Pointer);
    tkMethod:
      Result := SizeOf(TMethod);
    tkInt64:
      Result := 8;
    tkVariant:
                                                               
{$IFDEF CPUX64}
      Result := 32;
{$ELSE !CPUX64}
      Result := 16;
{$ENDIF !CPUX64}
    tkSet:
      begin
        Result := 4;
      end;
                               
  else
    Assert(False);
    Result := -1;
  end;
end;



function InterfaceDerivesFrom(TypeData: PTypeData; const GUID: TGUID): Boolean;
begin
  Result := True;
  while TypeData <> nil do
  begin
    if IsEqualGUID(TypeData^.Guid, GUID) then
       Exit;
    if (TypeData^.IntfParent <> nil) and (TypeData^.IntfParent^ <> nil) then
      TypeData := GetTypeData(TypeData^.IntfParent^)
    else
      Break;
  end;
  Result := False;
end;

function GetVariantType(TypeInfo: PTypeInfo): TVarType;
var
  TypeData: PTypeData;
begin
  case TypeInfo^.Kind of
    tkUnknown:  Result := varError;
                                                                                            
    tkInteger, tkChar, tkEnumeration, tkWChar:
      if (TypeInfo = System.TypeInfo(Boolean)) or
        (TypeInfo = System.TypeInfo(ByteBool)) or
        (TypeInfo = System.TypeInfo(WordBool)) or
        (TypeInfo = System.TypeInfo(LongBool)) then
        Result := varBoolean
      else
      begin
        TypeData := GetTypeData(TypeInfo);
        if TypeData^.MinValue >= 0 then
          if Cardinal(TypeData^.MaxValue) > $FFFF then
            Result := varLongWord
          else if TypeData^.MaxValue > $FF then
            Result := varWord
          else
            Result := varByte
        else
          if (TypeData^.MaxValue > $7FFF) or (TypeData^.MinValue < -$7FFF - 1) then
            Result := varInteger
          else if (TypeData^.MaxValue > $7F) or (TypeData^.MinValue < -$7F - 1) then
            Result := varSmallint
          else
            Result := varShortint;
      end;
    tkFloat:
      begin
        TypeData := GetTypeData(TypeInfo);
        case TypeData^.FloatType of
          ftSingle: Result := varSingle;
          ftDouble:
            if TypeInfo = System.TypeInfo(TDateTime) then
              Result := varDate
            else
              Result := varDouble;
          ftComp: Result := varInt64;
          ftCurr: Result := varCurrency;
        else
          Result := varError;
        end;
      end;
    tkString:   Result := varString;
    tkLString:  Result := varString;
    tkUString:  Result := varUString;
    tkWString:  Result := varOleStr;
    tkInterface:
      begin
        TypeData := GetTypeData(TypeInfo);
        if InterfaceDerivesFrom(TypeData, IDispatch) then
          Result := varDispatch
        else
          Result := varUnknown;
      end;
    tkVariant:  Result := varVariant;
    tkInt64:
      begin
        TypeData := GetTypeData(TypeInfo);
        if TypeData^.MinInt64Value >= 0 then
          Result := varUInt64
        else
          Result := varInt64;
      end;
    tkClass:    Result := varInteger;
                               
  else
    Result := varError;
  end;
end;




{$IFDEF MSWINDOWS}
type
  PParameterInfos = ^TParameterInfos;
  TParameterInfos = array[0..255] of ^PTypeInfo;

  TBaseMethodHandlerInstance = class
  protected
    TypeData: PTypeData;
    _PropertyType:TRttiProperty;
    ParamInfos: PParameterInfos;
    ParamOffsets: array of Word;
    StackSize: Integer;
{$IFDEF CPUX64}
    RegisterFlag: Word; // If bit is set, XMM is used. LSB is RCX.
{$ENDIF CPUX64}
    procedure InternalHandler(Params: Pointer);
    procedure Handler(Params: Pointer); virtual; abstract;
    procedure RegisterStub;
  public
    constructor Create(__PropType:TRttiProperty);
  end;

  TMethodHandlerInstance = class(TBaseMethodHandlerInstance)
  protected
    MethodHandler: IMethodHandler;
    procedure Handler(Params: Pointer); override;
  public
    constructor Create(const MethodHandler: IMethodHandler; __PropType:TRttiProperty);
  end;


function AdditionalInfoOf(TypeData: PTypeData): Pointer;
var
  P: PByte;
  I: Integer;
begin
  P := @TypeData^.ParamList;
  // Skip parameter names and types
  for I := 1 to TypeData^.ParamCount do
  begin
    Inc(P, 1 + P[1] + 1);
    Inc(P, P[0] + 1 );
  end;
  if TypeData^.MethodKind = mkFunction then
    // Skip return type name and info
    Inc(P, P[0] + 1 + 4);
  Result := P;
end;

function CreateMethodPointer(const MethodHandler: IMethodHandler; __PropType:TRttiProperty): TMethod;
begin

  TObject(Result.Data) := TMethodHandlerInstance.Create(MethodHandler, __PropType);
  Result.Code := @TMethodHandlerInstance.RegisterStub;
end;



procedure ReleaseMethodPointer(MethodPointer: TMethod);
begin
  TObject(MethodPointer.Data).Free;
end;


{ TBaseMethodHandlerInstance }

constructor TBaseMethodHandlerInstance.Create(__PropType:TRttiProperty);
{$IF Defined(CPUX86)} // No ASM blocks in here
var
  P: PByte;
  Offset: Integer;
  CurReg: Integer;
  I: Integer;
  Size: Integer;
begin
  Self._PropertyType := __PropType;



  Self.TypeData := __PropType.PropertyType.Handle.TypeData;

  P := AdditionalInfoOf(TypeData);
  ParamInfos := PParameterInfos(Cardinal(P) + 1);

  // Calculate stack size
  CurReg := paEDX;
  P := @TypeData^.ParamList;
  StackSize := 0;
  for I := 0 to TypeData^.ParamCount - 1 do
  begin
    if System.TypInfo.TParamFlags(P[0]) * [pfVar, pfConst, pfAddress, pfReference, pfOut] <> [] then
      Size := 4
    else
      Size := GetTypeSize(ParamInfos^[I]^);
    if (Size <= 4) and (CurReg <= paECX) then
      Inc(CurReg)
    else
    begin
      if Size < 4 then
        Size := 4;
      Inc(StackSize, Size);
    end;
    Inc(P, 1 + P[1] + 1);
    Inc(P, P[0] + 1);
  end;

  // Calculate parameter offsets
  SetLength(ParamOffsets, TypeData^.ParamCount);
  CurReg := paEDX;
  P := @TypeData^.ParamList;
  Offset := StackSize;
  for I := 0 to TypeData^.ParamCount - 1 do
  begin
    if System.TypInfo.TParamFlags(P[0]) * [pfVar, pfConst, pfAddress, pfReference, pfOut] <> [] then
      Size := 4
    else
      Size := GetTypeSize(ParamInfos^[I]^);
    if (Size <= 4) and (CurReg <= paECX) then
    begin
      ParamOffsets[I] := CurReg;
      Inc(CurReg);
    end
    else
    begin
      Dec(Offset, Size);
      ParamOffsets[I] := Offset;
    end;
    Inc(P, 1 + P[1] + 1);
    Inc(P, P[0] + 1);
  end;
end; // of CPUX86 implementation
{$ELSEIF Defined(CPUX64)}
var
  P: PByte;
  Offset: Integer;
  I: Integer;
begin
  Self._PropertyType := __PropType;
  Self.TypeData := __PropType.PropertyType.Handle.TypeData;

  P := AdditionalInfoOf(TypeData);
  ParamInfos := PParameterInfos(Cardinal(P) + 1);

  RegisterFlag := $00; // GPRs are default
  // Calculate stack size

  P := @TypeData^.ParamList;
  StackSize := 8;
  for I := 0 to TypeData^.ParamCount - 1 do
  begin
    if I < 3 then
    begin
      if (System.TypInfo.TParamFlags(P[0]) * [pfVar, pfConst, pfAddress, pfReference, pfOut] = [])
          and (ParamInfos^[I]^.Kind = tkFloat) then
        RegisterFlag := RegisterFlag or (1 shl (I+1));
    end;
    StackSize := StackSize + 8;
    Inc(P, 1 + P[1] + 1);
    Inc(P, P[0] + 1);
  end;
  if StackSize < 32 then
    StackSize := 32;

  // Calculate parameter offsets
  SetLength(ParamOffsets, TypeData^.ParamCount);
  P := @TypeData^.ParamList;
  Offset := 0;
  for I := 0 to TypeData^.ParamCount - 1 do
  begin
    Inc(Offset, 8);
    ParamOffsets[I] := Offset;
    Inc(P, 1 + P[1] + 1);
    Inc(P, P[0] + 1);
  end;
end; // of CPUX64 implementaiton }
{$ELSEIF defined(CPUARM32) or defined(CPUARM64)}
begin

end;
{$ELSE OTHERCPU}
{$MESSAGE Fatal 'Missing ObjectInvoke implementation'}
{$ENDIF OTHERCPU}

                                                                                                                
procedure TBaseMethodHandlerInstance.InternalHandler(Params: Pointer);
{$IF Defined(X86ASM)}
asm
        MOV     ECX,[EAX]
        JMP     DWORD PTR [ECX] + VMTOFFSET TMethodHandlerInstance.Handler
end;
{$ELSEIF Defined(X64ASM)}
asm
        MOV     EAX,[EAX]
        JMP     QWORD PTR [EAX] + VMTOFFSET TMethodHandlerInstance.Handler
end;
{$ELSEIF defined(CPUARM32) or defined(CPUARM64)}
begin
                                                                                         
end;
{$ELSE OTHERCPU}
{$MESSAGE Fatal 'Missing implementation'}
{$ENDIF OTHERCPU}

                                                                                            
procedure TBaseMethodHandlerInstance.RegisterStub;
{$IF Defined(X86ASM)}
const
  PtrSize = SizeOf(Pointer);
asm
        PUSH    EAX
        PUSH    ECX
        PUSH    EDX
        MOV     EDX,ESP
        CALL    InternalHandler
        // Pop EDX and ECX off the stack while preserving all registers.
        MOV     [ESP+4],EAX
        POP     EAX
        POP     EAX
        POP     ECX             // Self
        MOV     ECX,[ECX].TBaseMethodHandlerInstance.StackSize

        TEST    ECX,ECX
        JZ      @@SimpleRet
        // Jump to the actual return instruction since it is most likely not just a RET
        //JMP     ECX    // Data Exec. Prevention: Jumping into a GetMem allocated memory block

        // stack address alignment
        ADD     ECX, PtrSize - 1
        AND     ECX, NOT (PtrSize - 1)
        AND     ECX, $FFFF

        // clean up the stack
        PUSH    EAX                         // we need this register, so save it
        MOV     EAX,[ESP + 4]               // Load the return address
        MOV     [ESP + ECX + 4], EAX        // Just blast it over the first param on the stack
        POP     EAX
        ADD     ESP,ECX                     // This will move the stack back to where the moved
                                            // return address is now located. The next RET
                                            // instruction will do the final stack cleanup
@@SimpleRet:
end;
{$ELSEIF Defined(X64ASM)}
asm
        MOV     AX, WORD PTR [RCX].TBaseMethodHandlerInstance.RegisterFlag
@@FIRST:
        TEST    AX, $01
        JZ      @@SAVE_RCX
@@SAVE_XMM0:
        MOVSD   QWORD PTR [RSP+$08], XMM0
        JMP     @@SECOND
@@SAVE_RCX:
        MOV     QWORD PTR [RSP+$08], RCX

@@SECOND:
        TEST    AX, $02
        JZ      @@SAVE_RDX
@@SAVE_XMM1:
        MOVSD   QWORD PTR [RSP+$10], XMM1
        JMP     @@THIRD
@@SAVE_RDX:
        MOV     QWORD PTR [RSP+$10], RDX

@@THIRD:
        TEST    AX, $04
        JZ      @@SAVE_R8
@@SAVE_XMM2:
        MOVSD   QWORD PTR [RSP+$18], XMM2
        JMP     @@FORTH
@@SAVE_R8:
        MOV     QWORD PTR [RSP+$18], R8

@@FORTH:
        TEST    AX, $08
        JZ      @@SAVE_R9
@@SAVE_XMM3:
        MOVSD   QWORD PTR [RSP+$20], XMM3
        JMP     @@1
@@SAVE_R9:
        MOV     QWORD PTR [RSP+$20], R9

@@1:    LEA     RDX, QWORD PTR [RSP+$08]
        MOV     RAX, RCX
        SUB     RSP, $28
        CALL    InternalHandler
        ADD     RSP, $28
end;
{$ELSEIF defined(CPUARM32) or defined(CPUARM64)}
begin
                                                                                      
end;
{$ELSE OTHERCPU}
{$MESSAGE Fatal 'Missing implementation'}
{$ENDIF OTHERCPU}

{ TMethodHandlerInstance }

constructor TMethodHandlerInstance.Create(const MethodHandler: IMethodHandler;
  __PropType:TRttiProperty);
begin
  inherited Create(__PropType);
  Self.MethodHandler := MethodHandler;


end;

function GetCodePointer(Instance: TObject; P: Pointer): Pointer; inline;
begin
  if (IntPtr(P) and PROPSLOT_MASK) = PROPSLOT_VIRTUAL then // Virtual Method
    Result := PPointer(PNativeUInt(Instance)^ + (UIntPtr(P) and $FFFF))^
  else // Static method
    Result := P;
end;

{$IF Defined(X86ASM)}
procedure TMethodHandlerInstance.Handler(Params: Pointer);
var
  P: PByte;
  Parameters: PParameters;
  ReturnValue: Variant;
  I: Integer;
  Regs: array[paEAX..paEDX] of Cardinal;
  Offset: Integer;
  Data: Pointer;
  MethodValues: TArray<TValue>;
  Zerk:Integer;
  tmp:TValue;
begin
  Parameters := Params;

  Zerk := 0; // зеркальное отрожение!

  SetLength(MethodValues, TypeData^.ParamCount);

  // Fetch the parameters into ParamValues
  P := @TypeData^.ParamList;
  for I := 0 to TypeData^.ParamCount - 1 do
  begin
    Offset := ParamOffsets[I];
    if (Offset >= paEDX) and (Offset <= paECX) then
      Data := @Parameters^.Registers[Offset]
    else
      Data := @Parameters^.Stack[Offset];

    if ParamInfos^[I]^.Kind = tkClass then
      MethodValues[Zerk] := TObject(PPointer(Data)^)
    else if System.TypInfo.TParamFlags(P[0]) * [pfVar, pfOut] <> [] then
      TValue.Make(Pointer(PCardinal(Data)^), ParamInfos[I]^, MethodValues[Zerk])
    else
      TValue.Make(Data, ParamInfos[I]^, MethodValues[Zerk]);

    Inc(P, 1 + P[1] + 1);
    Inc(P, P[0] + 1);
    Inc(Zerk);
  end;
  // P is left pointing to the return type name if there is one.
           //   Self.MethodHandler.
  ReturnValue := MethodHandler.Execute(Self._PropertyType, MethodValues);

  P := @TypeData^.ParamList;
  for I := 0 to TypeData^.ParamCount - 1 do
  begin
    Offset := ParamOffsets[I];
    if (Offset >= paEDX) and (Offset <= paECX) then
      Data := @Parameters^.Registers[Offset]
    else
      Data := @Parameters^.Stack[Offset];

    if System.TypInfo.TParamFlags(P[0]) * [pfVar, pfOut] <> [] then
    begin
      // ~ = AV
      TValue.Make(Pointer(PCardinal(Data)^), ParamInfos[I]^, tmp);
      if(tmp.ToString <> MethodValues[I].ToString) then
         PPointer(PCardinal(Data)^)^ := PPointer(MethodValues[i].GetReferenceToRawData^)^;
    end;

    Inc(P, 1 + P[1] + 1);
    Inc(P, P[0] + 1);
  end;

  if TypeData^.MethodKind = mkFunction then
  begin
    Inc(P, P[0] + 1);
    ReturnValue := VarAsType(ReturnValue, GetVariantType(PPTypeInfo(P)^));
    if PPTypeInfo(P)^.Kind = tkFloat then
                                            
    else
    begin
      Regs[paEAX] := TVarData(ReturnValue).VLongWord;
      Regs[paEDX] := PCardinal(PByte(@TVarData(ReturnValue).VLongWord) + 4)^;
    end;
  end;

  // Let Stub procedures know where the RET instruction is
  asm
        MOV     EAX,DWORD PTR Regs[paEAX*4]
        MOV     EDX,DWORD PTR Regs[paEDX*4]
  end;
end; // of X86ASM implementation
{$ELSEIF Defined(X64ASM)}

procedure movret(Regers: prxarr);
// Let Stub procedures know where the RET instruction is
asm
  MOV     RAX,DWORD64 PTR Regers[paRAX*8]
  MOV     RDX,DWORD64 PTR Regers[paRDX*8]
end;

procedure TMethodHandlerInstance.Handler(Params: Pointer);
var
  P: PByte;
  Parameters: PParameters;
  ReturnValue: Variant;
  I: Integer;
  Regs: prxarr;
  Offset: Integer;
  Data: Pointer;
  MethodValues: TArray<TValue>;
  Zerk:Integer;
  tmp:TValue;
begin
  Parameters := Params;

  Zerk := 0; // зеркальное отрожение!

  SetLength(MethodValues, TypeData^.ParamCount);

  // Fetch the parameters into ParamValues
  P := @TypeData^.ParamList;

  for I := 0 to TypeData^.ParamCount - 1 do
  begin
    Offset := ParamOffsets[I];
    if (Offset >= paRDX) and (Offset <= paRCX) then
      Data := @Parameters^.Registers[Offset]
    else
      Data := @Parameters^.Stack[Offset];
    // ~ AV
    if ParamInfos^[I]^.Kind = tkClass then
      MethodValues[Zerk] := TObject(PPointer(Data)^)
    else if System.TypInfo.TParamFlags(P[0]) * [pfVar, pfOut] <> [] then
      TValue.Make(Pointer(PCardinal(Data)^), ParamInfos[I]^, MethodValues[Zerk])
    else
      TValue.Make(Data, ParamInfos[I]^, MethodValues[Zerk]);

    Inc(P, 1 + P[1] + 1);
    Inc(P, P[0] + 1);
    Inc(Zerk);
  end;
  // P is left pointing to the return type name if there is one.
           //   Self.MethodHandler.
  ReturnValue := MethodHandler.Execute(Self._PropertyType, MethodValues);

  P := @TypeData^.ParamList;
  for I := 0 to TypeData^.ParamCount - 1 do
  begin
    Offset := ParamOffsets[I];
    if (Offset >= paRDX) and (Offset <= paRCX) then
      Data := @Parameters^.Registers[Offset]
    else
      Data := @Parameters^.Stack[Offset];

    if System.TypInfo.TParamFlags(P[0]) * [pfVar, pfOut] <> [] then
    begin
      // ~ = AV
      TValue.Make(Pointer(PCardinal(Data)^), ParamInfos[I]^, tmp);
      if(tmp.ToString <> MethodValues[I].ToString) then
         PPointer(PCardinal(Data)^)^ := PPointer(MethodValues[i].GetReferenceToRawData^)^;
    end;

    Inc(P, 1 + P[1] + 1);
    Inc(P, P[0] + 1);
  end;

  if TypeData^.MethodKind = mkFunction then
  begin
    Inc(P, P[0] + 1);
    ReturnValue := VarAsType(ReturnValue, GetVariantType(PPTypeInfo(P)^));
    if PPTypeInfo(P)^.Kind = tkFloat then

    else
    begin
      Regs[paRAX] := TVarData(ReturnValue).VLongWord;
      Regs[paRDX] := PCardinal(PByte(@TVarData(ReturnValue).VLongWord) + 4)^;
    end;
  end;

  movret(Regs);
end;

{$ELSEIF defined(CPUARM32) or defined(CPUARM64)}
procedure TMethodHandlerInstance.Handler(Params: Pointer);
begin
                                                                             
end;
{$ELSE OTHERCPU}
{$MESSAGE Fatal 'Missing TMethodHandlerInstance.Handler implementation'}
{$ENDIF OTHERCPU}


{$ENDIF MSWINDOWS}

end.